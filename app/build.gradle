apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply from: "$rootDir/gradle/helpers/common-methods.gradle"

Properties semver = new Properties()
semver.load(new FileInputStream(file("semver.properties")))

def buildTimeMillis = System.currentTimeMillis()
def buildVersionCode = computeVersionCode(semver['major'], semver['minor'], semver['patch'], semver['build'])
def buildVersionName = computeVersionName(semver['major'], semver['minor'], semver['patch'])
def buildTime = computeBuildTimeFormatted(buildTimeMillis)
def buildRevision = computeRevision()


ant.propertyfile(file: "version.properties") {
    entry(key: "version", value: buildVersionName)
    entry(key: "versionCode", value: buildVersionCode)
    entry(key: "rev", value: buildRevision)
    entry(key: "buildTime", value: buildTime)
}


android {
    compileSdkVersion 27

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.tinashe.sdah"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode buildVersionCode
        versionName buildVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${versions.kotlin}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:appcompat-v7:${versions.support_library}"
    implementation "com.android.support:cardview-v7:${versions.support_library}"
    implementation "com.android.support:recyclerview-v7:${versions.support_library}"
    implementation "com.android.support:design:${versions.support_library}"
    implementation "android.arch.persistence.room:rxjava2:${versions.arch_comp}"
    annotationProcessor "android.arch.persistence.room:compiler:${versions.arch_comp}"
    implementation "com.google.dagger:dagger-android:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${versions.dagger}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${versions.dagger}"
    implementation("com.jakewharton:butterknife:${versions.butter_knife}") {
        exclude module: 'support-annotations'
    }
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.butter_knife}"
    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    implementation "com.github.bumptech.glide:recyclerview-integration:${versions.glide}"
    annotationProcessor "com.github.bumptech.glide:compiler:${versions.glide}"
    compile 'com.google.code.gson:gson:2.8.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
